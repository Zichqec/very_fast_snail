OnTranslate
{
	_talk = reference0
	
	//This is what makes %(embedded_elements) work in script input
	if reference1 == "" && reference2 == "" //If this is from the input box
	{ //send input box : no event (ref2) , no special flag (ref1)
		_talk = EVAL('"' + REPLACE(_talk,'"','""') + '"')
	}
	
	
	TOSTR(_talk)
}

OnAnchorSelect
{
	if "http://" _in_ reference0 || "https://" _in_ reference0; "\j[""%(reference0)""]"
}

OnKeyPress
{
	if reference0 == "f1"; "\![open,readme]"
	elseif reference0 == "t"; OnAiTalk
	elseif reference0 == "r"; OnLastTalk
}

OnSurfaceRestore
{
	"\1\s[-1]\0\s[0]"
}

OnWindowStateRestore
{
	"\1\s[-1]\0\s[0]"
}

On_homeurl
{
	"https://raw.githubusercontent.com/Zichqec/very_fast_snail/main/"
}

ghostver
{
	"1.0.0"
}

GenerateSnailChoices
{
	//ty terraria for inspiring me, but i'm using a different delimiter <3
	_id = ghostver + "|" + GenerateID
	_name = SnailNameGenerator(_id)
	_trait = traits(_name)
	
	"%(_id)%(C_BYTE1)%(_name)%(C_BYTE1)%(_trait)"
}

GenerateID : all
{
	"%(RAND(10))"
	"%(RAND(10))"
	"%(RAND(10))"
	"%(RAND(10))"
	"%(RAND(10))"
	"%(RAND(10))"
	"%(RAND(10))"
	"%(RAND(10))"
	"%(RAND(10))"
	"%(RAND(10))"
	"%(RAND(10))"
	"%(RAND(10))"
	"%(RAND(10))"
	"%(RAND(10))"
	"%(RAND(10))"
	"%(RAND(10))"
}

CreateSnail
{
	CurrentSnail.ID = _argv[0]
	CurrentSnail.Name = _argv[1]
	CurrentSnail.Version = ghostver
	CurrentSnail.Trait = _argv[2]
	CurrentSnail.TotalRaces = 0
	CurrentSnail.TotalRaceTime = 0
	CurrentSnail.HighestCoolness = 0
	CurrentSnail.TotalCrashes = 0
	CurrentSnail.Loadout = IARRAY
	CurrentSnail.DateSponsored = "%(year)-%(month)-%(day):%(GETSECCOUNT)"
}

SaveSnail
{
	_index = ASEARCH(_argv[0],SavedSnails)
	if _index == -1; _index = ARRAYSIZE(SavedSnails)
	
	_snaildata = ""
	_snaildata += /
	CurrentSnail.ID /
	+ C_BYTE1 + CurrentSnail.Name /
	+ C_BYTE1 + CurrentSnail.Version /
	+ C_BYTE1 + CurrentSnail.Trait /
	+ C_BYTE1 + CurrentSnail.TotalRaces /
	+ C_BYTE1 + CurrentSnail.TotalRaceTime /
	+ C_BYTE1 + CurrentSnail.HighestCoolness /
	+ C_BYTE1 + CurrentSnail.TotalCrashes /
	+ C_BYTE1 + CurrentSnail.Loadout /
	+ C_BYTE1 + CurrentSnail.DateSponsored
	
	SavedSnails[_index] =_snaildata
}

LoadSnail
{
	_index = ASEARCH(_argv[0],SavedSnails)
	if _index == -1; _index = 0
	
	_snaildata = SPLIT(SavedSnails[_index],C_BYTE1)
	
	CurrentSnail.ID = _snaildata[0]
	CurrentSnail.Name = _snaildata[1]
	CurrentSnail.Version = _snaildata[2]
	CurrentSnail.Trait = _snaildata[3]
	CurrentSnail.TotalRaces = TOINT(_snaildata[4])
	CurrentSnail.TotalRaceTime = TOINT(_snaildata[5])
	CurrentSnail.HighestCoolness = TOINT(_snaildata[6])
	CurrentSnail.TotalCrashes = TOINT(_snaildata[7])
	CurrentSnail.Loadout = SPLIT(_snaildata[8],",")
	CurrentSnail.DateSponsored = _snaildata[9]
}

CurrentSnail : all
{
	"\0\b[2]\_q"
	"I am a snail\n"
	"My ID is %(CurrentSnail.ID)\n"
	"My name is %(CurrentSnail.Name)\n"
	"I was made in version %(CurrentSnail.Version)\n"
	"My trait is \f[italic,1]""%(CurrentSnail.Trait)""\f[italic,default]\n"
	"I've run in %(CurrentSnail.TotalRaces) races\n"
	"My total time on the track is %(CurrentSnail.TotalRaceTime)\n"
	"My highest cool score ever is %(CurrentSnail.HighestCoolness)\n"
	"My current loadout is %(CurrentSnail.Loadout)\n"
	"I was sponsored on %(CurrentSnail.DateSponsored)\n"
	"\n\![*]\q[Done,OnBlank]"
}


OnNotifyDressupInfo
{
	Dressups.Current = IARRAY
	Dressups.Available = IARRAY
	
	foreach reference; _ref
	{
		if _ref[4,C_BYTE1] == "1"; Dressups.Current ,= _ref
		Dressups.Available ,= _ref
	}
}

//—————————————————————————————— Right click menu links ——————————————————————————————

//Takes an array of alternating names and urls, and formats them for use with the right click menu
FormatLinks : all
{
	for _i = 0; _i < ARRAYSIZE(_argv); _i++
	{
		_argv[_i] //Add the link/title
		//Alternate between adding %ASC(1) or %ASC(2)
		if _i % 2 == 1; "%ASC(2)"
		else; "%ASC(1)"
	}
}

On_sakura.recommendsites
{
	FormatLinks(recommendsites_sakura)
}

recommendsites_sakura : array
{
	"Link label";	"https://link.example.com/link/to/whatever/here/"
}

On_sakura.portalsites 
{
	FormatLinks(portalsites_sakura)
}

portalsites_sakura : array
{
	"Another link label";	"https://link.example.com/link/to/whatever/here/again/"
	"A third link label";	"https://link.example.com/link/to/whatever/a/third/time/you/can/add/as/many/as/you/want/"
}