OnFirstBoot : all
{
	stroke = 0
	LastTalk = ""
	SavedSnails.ID = IARRAY
	SavedSnails.Name = IARRAY
	SavedSnails.Version = IARRAY
	SavedSnails.Trait = IARRAY
	SavedSnails.TotalRaces = IARRAY
	SavedSnails.TotalRaceTime = IARRAY
	SavedSnails.HighestCoolness = IARRAY
	SavedSnails.TotalCrashes = IARRAY
	SavedSnails.Loadout = IARRAY
	SavedSnails.DateSponsored = IARRAY
	SavedSnails.Notes = IARRAY
	
	CurrentSnail.ID = ""
	CurrentSnail.Name = ""
	CurrentSnail.Version = ""
	CurrentSnail.Trait = ""
	CurrentSnail.TotalRaces = 0
	CurrentSnail.TotalRaceTime = 0
	CurrentSnail.HighestCoolness = 0
	CurrentSnail.TotalCrashes = 0
	CurrentSnail.Loadout = IARRAY
	CurrentSnail.DateSponsored = ""
	CurrentSnail.Notes = IARRAY
	
	HighestAllTimeCoolness = 0
	
	PurchasedItems = IARRAY
	PurchasedItems ,= "Body Paint,Beige Basic"
	PurchasedItems ,= "Shell Paint,Brown Basic"
	Lettuce = 100
	
	EquipFilter = "All"
	
	SnailPickList = (GenerateSnailChoices,GenerateSnailChoices,GenerateSnailChoices)
	
	//error logging stuff because i think tama is causing me trouble...
	ErrorsToPush.level = IARRAY
	ErrorsToPush.description = IARRAY
	
	
	
	"\1\s[-1]\0\s[0]"
	--
	OnFirst.SnailPick
}

OnFirst.SnailPick : all
{
	"\0\b[0]\t\*\_q\![set,autoscroll,disable]"
	"\f[bold,1]\f[italic,1]\f[align,center]\f[height,+2]CHOOSE YOUR RACER!\n\f[height,default]\f[align,left]\f[italic,default]\f[bold,default]\n"
	
	
	foreach SnailPickList; _snail
	{
		_id = _snail[0,C_BYTE1]
		_name = _snail[1,C_BYTE1]
		_trait = _snail[2,C_BYTE1]
		"\![*]\__q[OnFirst.SelectSnail,""%(_id)"",""%(_name)"",""%(_trait)""]%(_name)\__q\n"
	}
	"\n"
	"\![*]\__q[OnFirst.NewSnailList]None of these, show me other snails\__q"
}

OnFirst.SelectSnail : all
{
	_name = reference1
	_trait = reference2
	
	"\0\b[2]\t\*"
	"%(_trait)\n\n\_q"
	"\![*]\__q[OnFirst.SnailPick]Back\__q    \![*]\__q[OnFirst.SelectSnail.Confirm,""%(reference0)"",""%(reference1)"",""%(reference2)""]Choose this snail\__q"
}

OnFirst.SelectSnail.Confirm : all
{
	CreateSnail(reference0,reference1,reference2)
	RefreshDressups
	OnFirst.SelectSnail.Confirm.SnailTalk
}

OnFirst.SelectSnail.Confirm.SnailTalk
{
	"\1\b[10]Racing ready!"
	"\1\b[10]Let's land lettuce."
	"\1\b[10]Speed scores stardom."
	"\1\b[10]Acknowledged. Acclaim assured."
	"\1\b[10]Bring Back Bagel Bites."
}

OnFirst.NewSnailList
{
	SnailPickList = (GenerateSnailChoices,GenerateSnailChoices,GenerateSnailChoices)
	OnFirst.SnailPick
}


OnInitialize
{
	if reference0 != "reload"
	{
		LastTalk = ""
	}
}

OnBoot
{
	"\p[2]\s[-1]\![move,--x=0,--y=0,--base=0,--option=ignore-sticky-window]\1\s[-1]\![move,--x=0,--y=0,--base=0,--option=ignore-sticky-window]"
	--
	"\0\s[0]"
	--
	LegalityCheck
	--
	"\1\b[20]Welcome back to \f[italic,1]Speed of Sound\f[italic,default] radio, your #1 source of Snail Racing news!"
}

OnClose
{
	"\1\b[20]That's all for today's broadcast. Until next time, racers!"
	"\1\b[10]Sleepy snails snooze soundly."
	--
	"\w8\w8\-"
}