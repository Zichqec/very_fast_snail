OnFirstBoot : all
{
	stroke = 0
	LastTalk = ""
	SavedSnails = IARRAY
	
	CurrentSnail.ID = ""
	CurrentSnail.Name = ""
	CurrentSnail.Version = ""
	CurrentSnail.Trait = ""
	CurrentSnail.TotalRaces = 0
	CurrentSnail.TotalRaceTime = 0
	CurrentSnail.HighestCoolness = 0
	CurrentSnail.TotalCrashes = 0
	CurrentSnail.Loadout = IARRAY
	CurrentSnail.DateSponsored = ""
	
	SnailPickList = (GenerateSnailChoices,GenerateSnailChoices,GenerateSnailChoices)
	
	"\1\s[-1]\0\s[0]"
	--
	OnFirst.SnailPick
}

OnFirst.SnailPick : all
{
	"\_q\f[bold,1]\f[italic,1]\f[align,center]\f[height,+2]CHOOSE YOUR RACER!\n\f[height,default]\f[align,left]\f[italic,default]\f[bold,default]\n"
	
	
	foreach SnailPickList; _snail
	{
		_id = _snail[0,C_BYTE1]
		_name = _snail[1,C_BYTE1]
		_trait = _snail[2,C_BYTE1]
		"\![*]\__q[OnFirst.SelectSnail,""%(_id)"",""%(_name)"",""%(_trait)""]%(_name)\__q\n"
	}
	"\n"
	"\![*]\__q[OnFirst.NewSnailList]None of these, show me other snails\__q"
}

OnFirst.SelectSnail : all
{
	_name = reference1
	_trait = reference2
	
	"\0\b[2]\t\*"
	"%(_trait)\n\n\_q"
	"\![*]\__q[OnFirst.SnailPick]Back\__q    \![*]\__q[OnFirst.SelectSnail.Confirm,""%(reference0)"",""%(reference1)"",""%(reference2)""]Choose this snail\__q"
}

OnFirst.SelectSnail.Confirm
{
	CreateSnail(reference0,reference1,reference2)
	"Snail picked: %(reference)"
}

OnFirst.NewSnailList
{
	SnailPickList = (GenerateSnailChoices,GenerateSnailChoices,GenerateSnailChoices)
	OnFirst.SnailPick
}


OnInitialize
{
	if reference0 != "reload"
	{
		LastTalk = ""
	}
}

OnBoot
{
	"\1\s[-1]\0\s[0]"
	--
	"snails"
}

OnClose
{
	"\0\s[0]Farewell."
	"\0\s[0]Goodbye."
	--
	"\w8\w8\-"
}