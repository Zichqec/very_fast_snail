OnFirstBoot : all
{
	stroke = 0
	LastTalk = ""
	SavedSnails.ID = IARRAY
	SavedSnails.Name = IARRAY
	SavedSnails.Version = IARRAY
	SavedSnails.Trait = IARRAY
	SavedSnails.TotalRaces = IARRAY
	SavedSnails.TotalRaceTime = IARRAY
	SavedSnails.HighestCoolness = IARRAY
	SavedSnails.TotalCrashes = IARRAY
	SavedSnails.Loadout = IARRAY
	SavedSnails.DateSponsored = IARRAY
	SavedSnails.Notes = IARRAY
	SavedSnails.Training = IARRAY //Added in whatever version we call this
	SavedSnails.PersonalityType = IARRAY
	SavedSnails.PoetryType = IARRAY
	SavedSnails.RadioPreference = IARRAY
	SavedSnails.AltHobby = IARRAY
	SavedSnails.Cleanliness = IARRAY
	SavedSnails.LastOut = IARRAY
	
	CurrentSnail.ID = ""
	CurrentSnail.Name = ""
	CurrentSnail.Version = ""
	CurrentSnail.Trait = ""
	CurrentSnail.TotalRaces = 0
	CurrentSnail.TotalRaceTime = 0
	CurrentSnail.HighestCoolness = 0
	CurrentSnail.TotalCrashes = 0
	CurrentSnail.Loadout = IARRAY
	CurrentSnail.DateSponsored = ""
	CurrentSnail.Notes = IARRAY
	CurrentSnail.Training = (0,0,0,0,0,0,0,0,0,0)
	CurrentSnail.PersonalityType = ""
	CurrentSnail.PoetryType = ""
	CurrentSnail.RadioPreference = ""
	CurrentSnail.AltHobby = ""
	CurrentSnail.Cleanliness = 0
	CurrentSnail.LastOut = GETSECCOUNT
	
	HighestAllTimeCoolness = 0
	
	PurchasedItems = IARRAY
	PurchasedItems ,= "Body Paint,Beige Basic"
	PurchasedItems ,= "Shell Paint,Brown Basic"
	Lettuce = 100
	
	EquipFilter = "All"
	
	SnailPickList = (GenerateSnailChoices,GenerateSnailChoices,GenerateSnailChoices)
	
	//error logging stuff because i think tama is causing me trouble...
	ErrorsToPush.level = IARRAY
	ErrorsToPush.description = IARRAY
	
	SpongePurchased.Basic = 0
	SpongePurchased.Better = 0
	
	SnailTrainingTimer = (-1,-1) //start time, duration
	
	Racing = 0
	
	RadioStation = "Racing News"
	RadioScan = 3600 //1hr default scan time
	RadioScanTime = GETSECCOUNT
	
	
	
	"\1\s[-1]\0\s[0]"
	--
	OnFirst.SnailPick
}

OnInitialize
{
	if reference0 != "reload"
	{
		LastTalk = ""
		Racing = 0 //may need to move
		if RadioScan == -1 && RadioStation != "Off"; RadioStationChange //-1 means that it is to change on boot
		if GETSECCOUNT > CurrentSnail.LastOut + 600; CurrentSnail.Cleanliness = 0 //10 minute reset
		CurrentSnail.LastOut = GETSECCOUNT //this really only matters in the case of crashes i think...
	}
	
	if !ISVAR("SpongePurchased.Basic"); SpongePurchased.Basic = 0
	if !ISVAR("SpongePurchased.Better"); SpongePurchased.Better = 0
	if !ISVAR("CurrentSnail.Cleanliness"); CurrentSnail.Cleanliness = 0
	
	if !ISVAR("SavedSnails.Training")
	{
		for _i = 0; _i < ARRAYSIZE(SavedSnails.ID); _i++
		{
			SavedSnails.Training[_i] = (0,0,0,0,0,0,0,0,0,0)
		}
	}
	if !ISVAR("CurrentSnail.Training") //To be doubly sure...
	{
		CurrentSnail.Training = (0,0,0,0,0,0,0,0,0,0)
	}
	
	//TODO this is gonna get replaced anyways because I want to save them as files for sure now...
	_snailsonality = GenerateSnailsonality(CurrentSnail.Name,CurrentSnail.ID)
	if CurrentSnail.PersonalityType == ""; CurrentSnail.PersonalityType = _snailsonality[0]
	if CurrentSnail.PoetryType == ""; CurrentSnail.PoetryType = _snailsonality[1]
	if CurrentSnail.RadioPreference == ""; CurrentSnail.RadioPreference = _snailsonality[2]
	if CurrentSnail.AltHobby == ""; CurrentSnail.AltHobby = _snailsonality[3]
	
	if !ISVAR("RadioStation"); RadioStation = "Racing News"
	if !ISVAR("RadioScan"); RadioScan = 3600
	if !ISVAR("RadioScanTime"); RadioScanTime = GETSECCOUNT
}

OnDestroy
{
	if reference0 != "reload"
	{
		CurrentSnail.LastOut = GETSECCOUNT
	}
}

OnFirst.SnailPick : all
{
	"\0\b[0]\t\*\_q\![set,autoscroll,disable]\![no-autopause]"
	"%(bigheader('CHOOSE YOUR RACER!'))"
	
	
	foreach SnailPickList; _snail
	{
		_id = _snail[0,C_BYTE1]
		_name = _snail[1,C_BYTE1]
		_trait = _snail[2,C_BYTE1]
		"\![*]\__q[OnFirst.SelectSnail,""%(_id)"",""%(_name)"",""%(_trait)""]%(_name)\__q\n"
	}
	"\n"
	"\![*]\__q[OnFirst.NewSnailList]None of these, show me other snails\__q"
}

OnFirst.SelectSnail : all
{
	_name = reference1
	_trait = reference2
	
	"\0\b[2]\t\*"
	"%(_trait)\n\n\_q" //TODO names in the traits that have dots will get autopaused...
	"\![*]\__q[OnFirst.SnailPick]Back\__q    \![*]\__q[OnFirst.SelectSnail.Confirm,""%(reference0)"",""%(reference1)"",""%(reference2)""]Choose this snail\__q"
}

OnFirst.SelectSnail.Confirm : all
{
	CreateSnail(reference0,reference1,reference2)
	RefreshDressups
	OnFirst.SelectSnail.Confirm.SnailTalk
}

OnFirst.SelectSnail.Confirm.SnailTalk
{
	PositionInvisibleSnails
	--
	"\1\b[10]Racing ready!"
	"\1\b[10]Let's land lettuce."
	"\1\b[10]Speed scores stardom."
	"\1\b[10]Acknowledged. Acclaim assured."
	"\1\b[10]Bring back bagel bites."
}

OnFirst.NewSnailList
{
	SnailPickList = (GenerateSnailChoices,GenerateSnailChoices,GenerateSnailChoices)
	OnFirst.SnailPick
}

OnBoot
{
	PositionInvisibleSnails
	--
	spongeoff
	--
	"\0\s[0]"
	--
	LegalityCheck
	--
	OnCorrectMoonwalk
	--
	"\1\b[20]Welcome back to \f[italic,1]Speed of Sound\f[italic,default] radio, your #1 source of Snail Racing news!"
}

OnClose
{
	"\1\b[20]That's all for today's broadcast. Until next time, racers!"
	"\1\b[10]Sleepy snails snooze soundly."
	--
	"\w8\w8\-"
}