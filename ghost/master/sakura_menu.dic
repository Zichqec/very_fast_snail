SakuraMenuGreetings : nonoverlap
{
	"Snail."
}

//Because there is an all modifier here, you don't need to write -- between menu options
OnSakuraMenu : all
{
	"\0\s[0]\b2\![set,autoscroll,disable]"
	if SHIORI3FW.Eventid == "OnMouseDoubleClick"; SakuraMenuGreetings //The check here is so they won't comment if you click the back button. Remove if you prefer
	
	"\n\n\_q"
	
	"\![*]\q[Talk to me,OnAiTalk]"
	if LastTalk != ""; "  \![*]\q[Say that again,OnLastTalk]"
	"\n\n"
	
	"\![*]\q[Change loadout,OnEquipMenu,bodypaint]\n\n"
	"\![*]\q[Snail shop,OnShopMenu,bodypaint]\n\n"
	
	
	"\![*]\q[Change talk rate,OnTalkRateMenu]\n\n"
	
	if SHIORI3FW.DebugMode
	{
		"\![*]\q[Change snails,OnDebug.SnailPicker]\n\n"
	}
	
	"\![*]\q[Nevermind,blank]"	
}

OnLastTalk
{
	LastTalk
}

OnEquipMenu : all
{
	_category = reference0
	if _category == ""; _category = "bodypaint"
	
	"\C\![lock,balloonrepaint]\c\0\b[2]\t\*\![no-autopause]\_q"
	
	foreach Dressups.Categories; _cat
	{
		"\__q[OnEquipMenu,%(_cat)]%(_cat)\__q  "
	}
	"\n\n"
	
	foreach Dressups.Available; _dressup
	{
		_dressup = SPLIT(_dressup,C_BYTE1)
		//character, category, part, options, on/off, thumbnail
		if _dressup[1] == _category && IsOwned(_dressup[1],_dressup[2])
		{
			if _dressup[4] == "1"; "\![*]"
			"\__q[OnEquipMenu.ToggleItem,""%(_dressup[1])"",""%(_dressup[2])""]%(_dressup[2])\__q\n"
		}
	}
	"\n"
	"\![*]\__q[OnEquipMenu.Confirm]Done\__q\![unlock,balloonrepaint]"
}

OnEquipMenu.ToggleItem : all
{
	"\C\![lock,balloonrepaint]\c\0\b[0]\_q\![bind,""%(reference0)"",""%(reference1)""] "
	"\![raise,OnEquipMenu,""%(reference0)""]"
}

OnEquipMenu.Confirm : all
{
	if CurrentSnail.Loadout == Dressups.Current
	{
		OnSakuraMenu
		return
	}
	
	"\0\b[0]\_q\t\*\![no-autopause]"
	"Save the current loadout?\n\n"
	"\![*]\__q[OnEquipMenu.Save]Yes, save this loadout\__q\n\n"
	"\![*]\__q[OnEquipMenu.Revert]No, go back to the previous loadout\__q"
}

OnEquipMenu.Save : all
{
	//TODO legality check
	CurrentSnail.Loadout = Dressups.Current
	RefreshDressups
	OnSakuraMenu
}

OnEquipMenu.Revert : all
{
	"\0"
	RefreshDressups
}

OnShopMenu : all
{
	_category = reference0
	if _category == ""; _category = "bodypaint"
	
	"\C\![lock,balloonrepaint]\c\0\b[2]\t\*\![no-autopause]\_q"
	
	foreach Dressups.Categories; _cat
	{
		"\__q[OnShopMenu,%(_cat)]%(_cat)\__q  "
	}
	"\n\n"
	
	foreach ItemDetails; _item
	{
		_purchased = IsOwned(_item[0],_item[1])
		//Category,Name,Unlocked,Price,Speed,Coolness,Explosiveness
		if _item[0] == _category && (_item[2] == "1" || _purchased)
		{
			_choice = "\__q[OnShopMenu.ToggleItem,""%(_item[0])"",""%(_item[1])""]%(_item[1])\__q"
			
			if ASEARCH("%(_item[0]),%(_item[1])",Dressups.Current) != -1; "\![*]"
			
			switch _purchased
			{
				"%(_choice) %(L)%(_item[3])"
				"\f[color,disabled]%(_choice) (purchased)\f[color,default]"
			}
			"\n"
		}
	}
	"\n"
	"\![*]\__q[OnShopMenu.Confirm]Done\__q\![unlock,balloonrepaint]"
	
	"\1\b[0]\![lock,balloonrepaint]\c"
	
	_stats = CurrentStats
	_speed = _stats[0]
	_coolness = _stats[1]
	_explosiveness = _stats[2]
	_cost = _stats[3]
	
	"Cost: %(L)%(_cost)\n"
	"Speed: %(_speed)\n"
	"Coolness: %(_coolness)\n"
	"Explosiveness: %(_explosiveness)\n"
	
	"\![unlock,balloonrepaint]\0"
}

OnShopMenu.ToggleItem : all
{
	"\C\![lock,balloonrepaint]\c\0\b[0]\_q\![bind,""%(reference0)"",""%(reference1)""] "
	"\![raise,OnShopMenu,""%(reference0)""]"
}

OnShopMenu.Confirm : all
{
	if CurrentSnail.Loadout == Dressups.Current
	{
		OnSakuraMenu
		return
	}
	
	_cost = 0
	_count = 0
	foreach Dressups.Current; _dressup
	{
		if !IsOwned(_dressup[0],_dressup[1])
		{
			foreach ItemDetails; _item
			{
				if _item[0] == _dressup[0] && _item[1] == _dressup[1]
				{
					_count++
					_cost += TOINT(_item[3])
				}
			}
		}
	}
	
	_s = ""
	_thisitem = "this item"
	if _count != 1
	{
		_s = "s"
		_thisitem = "these items"
	}
	
	"\0\b[0]\_q\t\*\![no-autopause]"
	
	if _count == 0
	{
		"Save the current loadout?\n\n"
		"\![*]\__q[OnEquipMenu.Save]Yes, save this loadout\__q\n\n"
		"\![*]\__q[OnEquipMenu.Revert]No, go back to the previous loadout\__q"
	}
	elseif _cost > Lettuce
	{
		"The cost of %(_count) item%(_s) comes to %(_cost) lettuce. You can't afford %(_thisitem).\n\n"
		"\![*]\__q[OnShopMenu]Back to the shop\__q\n\n"
		"\![*]\__q[OnShopMenu.Revert]Nevermind, I'll shop another time\__q"
	}
	else
	{
		"Purchase %(_count) item%(_s) for %(_cost) lettuce?\n\n"
		"\![*]\__q[OnShopMenu.Save,keep,%(_cost)]Yes, purchase %(_thisitem) and save this loadout\__q\n\n"
		"\![*]\__q[OnShopMenu.Save,leave,%(_cost)]Yes, purchase %(_thisitem) but use previous loadout\__q\n\n"
		"\![*]\__q[OnShopMenu]No, go back to shopping\__q\n\n"
		"\![*]\__q[OnShopMenu.Revert]No, go back to the previous loadout\__q"
	}
}

OnShopMenu.Save : all
{
	Lettuce -= TOINT(reference1)
	
	foreach Dressups.Current; _dressup
	{
		if !IsOwned(_dressup[0],_dressup[1])
		{
			foreach ItemDetails; _item
			{
				if _item[0] == _dressup[0] && _item[1] == _dressup[1]
				{
					PurchasedItems ,= "%(_item[0]),%(_item[1])"
				}
			}
		}
	}
	
	//TODO legality check
	if reference0 == "keep"
	{
		CurrentSnail.Loadout = Dressups.Current
	}
	else //leave
	{
		RefreshDressups
	}
	OnSakuraMenu
}

OnShopMenu.Revert : all
{
	"\0"
	RefreshDressups
}

RefreshDressups : all
{
	LOGGING("====================================================================")
	foreach Dressups.Current; _dressup
	{
		"\![bind,%(_dressup[0]),%(_dressup[1]),0]"
	}
	foreach CurrentSnail.Loadout; _dressup
	{
		"\![bind,%(_dressup[0]),%(_dressup[1]),1]"
	}
	_coolness = TOINT(CurrentStats[1])
	LOGGING("_coolness: %(_coolness)")
	if _coolness > HighestAllTimeCoolness; HighestAllTimeCoolness = _coolness
	if _coolness > CurrentSnail.HighestCoolness; CurrentSnail.HighestCoolness = _coolness
}

OnTalkRateMenu : all
{	
	//This is just setting up a label, you may need to add to it or tweak it if you change what rates are available
	_talkrate = "Off"
	if aitalkinterval == 30; _talkrate = "30 seconds"
	elseif aitalkinterval == 60; _talkrate = "1 minute"
	elseif aitalkinterval == 180; _talkrate = "3 minutes"
	elseif aitalkinterval == 300; _talkrate = "5 minutes"
	elseif aitalkinterval == 600; _talkrate = "10 minutes"
	
	"\0\s[0]\b2\![set,autoscroll,disable]\_q"
	"Current interval: %(_talkrate)\n\n"
	
	"\![*]\q[Off,OnTalkRateChange,0]\n"
	"\![*]\q[30 seconds,OnTalkRateChange,30]\n"
	"\![*]\q[1 minute,OnTalkRateChange,60]\n"
	"\![*]\q[3 minutes,OnTalkRateChange,180]\n"
	"\![*]\q[5 minutes,OnTalkRateChange,300]\n"
	"\![*]\q[10 minutes,OnTalkRateChange,600]\n"
	"\n"
	"\![*]\q[Back,OnSakuraMenu]  \![*]\q[Nevermind,blank]"
}

OnTalkRateChange
{
	aitalkinterval = TOINT(reference0)
}

OnDebug.SnailPicker : all
{
	"\0\b[2]\_q\![no-autopause]"
	for _i = 0; _i < ARRAYSIZE(SavedSnails.ID); _i++
	{
		if SavedSnails.ID[_i] == CurrentSnail.ID; "\![*]"
		"\__q[OnDebug.SnailPicker.Select,""%(SavedSnails.ID[_i])""]%(SavedSnails.Name[_i])\__q\n"
	}
	"\n\![*]\q[See snail stats,OnDebug.CurrentSnail]"
	"\n\n\![*]\q[Add a snail,OnFirst.SnailPick]"
	
	"\n\n\![*]\q[Back,OnSakuraMenu]"
}

OnDebug.SnailPicker.Select
{
	SaveSnail(CurrentSnail.ID)
	LOGGING("SAVING %(CurrentSnail.ID) %(CurrentSnail.Name)")
	LoadSnail(reference0)
	LOGGING("LOADING %(CurrentSnail.ID) %(CurrentSnail.Name)")
	
	"\C%(RefreshDressups)\![raise,OnDebug.SnailPicker]"
}

OnDebug.CurrentSnail : all
{
	"\0\b[2]\_q"
	"I am a snail\n"
	"My ID is %(CurrentSnail.ID)\n"
	"My name is %(CurrentSnail.Name)\n"
	"I was made in version %(CurrentSnail.Version)\n"
	"My trait is \f[italic,1]""%(CurrentSnail.Trait)""\f[italic,default]\n"
	"I've run in %(CurrentSnail.TotalRaces) races\n"
	"My total time on the track is %(CurrentSnail.TotalRaceTime)\n"
	"My highest cool score ever is %(CurrentSnail.HighestCoolness)\n"
	"My current loadout is %(CurrentSnail.Loadout)\n"
	"I was sponsored on %(CurrentSnail.DateSponsored)\n"
	"\n\![*]\q[Back,OnDebug.SnailPicker]  \![*]\q[Done,OnBlank]"
}